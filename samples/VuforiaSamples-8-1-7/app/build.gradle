apply plugin: 'com.android.application'

def VUFORIA_SDK_DIR = '../../../'

android {
    compileSdkVersion 28
    buildToolsVersion "28.0.3"

    sourceSets.main {
        jni.srcDirs = []
        jniLibs.srcDir "$VUFORIA_SDK_DIR/build/lib/"
    }

    defaultConfig {
        applicationId "com.vuforia.engine.CoreSamples"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 810
        versionName "8.1"
        vectorDrawables.useSupportLibrary = true

        ndk {
            // Default ABI list for this app, can be over-ridden by providing an abiList property
            // e.g. gradle -PabiList=x86 clean assembleDebug
            abiFilters ((project.findProperty('abiList') ?: 'armeabi-v7a, arm64-v8a, x86').split(/,\s*/))
        }
    }

    archivesBaseName = rootProject.projectDir.getName()

    // Ensure that Vuforia Engine datasets placed in the assets folder do not get compressed as
    // this can significantly increase their loading time
    aaptOptions {
        noCompress 'dat'
    }

    buildTypes {
        release {
            minifyEnabled false
        }
        debug {
            minifyEnabled false
            debuggable true
        }
    }
}

dependencies {
    implementation files("$VUFORIA_SDK_DIR/build/java/Vuforia/Vuforia.jar")
    // ARCore library
    implementation 'com.google.ar:core:1.6.0'
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.android.support:support-v4:28.0.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    implementation 'com.android.support:recyclerview-v7:28.0.0'
    implementation 'com.android.support:design:28.0.0'
    implementation files('libs/core-3.3.0.jar')
}
